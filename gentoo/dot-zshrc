# ~/.zshrc
# Interactive shell configuration.

[[ -o interactive ]] || return

# fpath: add what exists (Arch/Gentoo differences are absorbed here)
for d in \
  /usr/share/zsh/site-functions \
  /usr/share/zsh/plugins \
; do
  [[ -d "$d" ]] && fpath=("$d" $fpath)
done

autoload -Uz compinit
# Use XDG cache for compdump
_compdump="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/zcompdump"
mkdir -p -- "${_compdump:h}" 2>/dev/null
compinit -d "${_compdump}"

# Key timeout (zsh: hundredths of a second)
KEYTIMEOUT=5

# setopt
setopt interactivecomments

# History (XDG)
HISTFILE="${XDG_STATE_HOME:-$HOME/.local/state}/zsh/history"
mkdir -p -- "${HISTFILE:h}" 2>/dev/null
HISTSIZE=10000
SAVEHIST=10000
setopt APPEND_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_REDUCE_BLANKS
setopt SHARE_HISTORY

# Hooks
autoload -Uz add-zsh-hook

# Keep GPG_TTY fresh and inform gpg-agent (safe for interactive)
_gpg_update_tty() {
  export GPG_TTY="${TTY:-$(tty 2>/dev/null)}"
  command -v gpg-connect-agent >/dev/null 2>&1 || return 0
  gpg-connect-agent updatestartuptty /bye >/dev/null 2>&1 || true
}
add-zsh-hook precmd _gpg_update_tty

# vcs_info for Git status in prompt
autoload -Uz vcs_info
setopt PROMPT_SUBST

zstyle ':vcs_info:*' enable git

# %c/%u を出す
zstyle ':vcs_info:git:*' check-for-changes true

# formats / actionformats の両方に %c%u%m を入れる（action時に欠けないように）
zstyle ':vcs_info:git:*' formats       ' on %F{yellow}%b%f%c%u%m'
zstyle ':vcs_info:git:*' actionformats ' on %F{yellow}%b%f (%F{magenta}%a%f)%c%u%m'

zstyle ':vcs_info:git:*' stagedstr     ' %F{green}●%f'
zstyle ':vcs_info:git:*' unstagedstr   ' %F{red}●%f'

# ★untracked を検出する hook
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
+vi-git-untracked() {
  [[ "$1" == "1" ]] || return 0

  local untracked
  untracked=$(command git ls-files --others --exclude-standard 2>/dev/null | command head -n 1)

  if [[ -n "$untracked" ]]; then
    hook_com[misc]=' %F{magenta}●%f'   # ← untracked 
  else
    hook_com[misc]=''
  fi
}

add-zsh-hook precmd vcs_info

# ---- command duration on RPROMPT (>= 3s) ------------------------------------
zmodload -F zsh/datetime b:EPOCHREALTIME 2>/dev/null || true

typeset -gF __cmd_start=0.0

_cmdtimer_preexec() {
  if (( ${+EPOCHREALTIME} )); then
    __cmd_start=$EPOCHREALTIME
  else
    __cmd_start=$SECONDS
  fi
}

_cmdtimer_precmd() {
  local -F elapsed=0.0
  local out
  local -i total h m s

  # default: show nothing
  RPROMPT=''

  (( __cmd_start > 0 )) || return

  if (( ${+EPOCHREALTIME} )); then
    elapsed=$(( EPOCHREALTIME - __cmd_start ))
  else
    elapsed=$(( SECONDS - __cmd_start ))
  fi
  __cmd_start=0.0

  (( elapsed >= 3.0 )) || return

  total=$(( elapsed + 0.5 ))   # 四捨五入（切り捨てなら total=$(( elapsed ))

  h=$(( total / 3600 ))
  m=$(( (total % 3600) / 60 ))
  s=$(( total % 60 ))

  if (( h > 0 )); then
    printf -v out '%dh%02dm%02ds' $h $m $s
  elif (( m > 0 )); then
    printf -v out '%dm%02ds' $m $s
  else
    printf -v out '%.1fs' $elapsed
  fi

  RPROMPT="%F{cyan}${out}%f"
}

add-zsh-hook preexec _cmdtimer_preexec
add-zsh-hook precmd  _cmdtimer_precmd

# コマンド行の右端に RPROMPT が「残る」のが嫌なら有効化（おすすめ）
setopt transient_rprompt
# -----------------------------------------------------------------------------

PROMPT='%F{green}%n%f@%F{blue}%m%f:%~${vcs_info_msg_0_}
%# '

# zsh-autosuggestions (path varies by distro)
for f in \
  /usr/share/zsh/site-functions/zsh-autosuggestions.zsh \
  /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh \
; do
  [[ -r "$f" ]] && source "$f" && break
done

# zsh-syntax-highlighting should be last
for f in \
  /usr/share/zsh/site-functions/zsh-syntax-highlighting.zsh \
  /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh \
; do
  [[ -r "$f" ]] && source "$f" && break
done

# tmux 外で、かつ対話シェルのときのみtmuxを起動
if [[ -z "$TMUX" && -n "$PS1" ]]; then
  tmux attach || tmux new
fi
